plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // mandatory dependencies for using Spock
    implementation platform('org.apache.groovy:groovy-bom:4.0.20')
    implementation 'org.apache.groovy:groovy'
    testImplementation platform("org.spockframework:spock-bom:2.4-M1-groovy-4.0")
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.spockframework:spock-spring'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // Query DSL
    implementation('com.querydsl:querydsl-mongodb:5.1.0') {
        // Exclude outdated dependency.
        // org.mongodb.mongo-java-driver is relocated to org.mongodb.mongodb-driver-sync
        // which is already bundled with org.springframework.boot:spring-boot-starter-data-mongodb.
        // This outdated dependency causes NoSuchMethodException com.mongodb.client.MongoCursor.available()
        // when call MongoTemplate.find() method.
        exclude module: 'mongo-java-driver'
    }
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:general'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:spock:1.19.7'
    testImplementation 'org.testcontainers:mongodb:1.19.7'
}

tasks.named('test') {
    useJUnitPlatform()
}
